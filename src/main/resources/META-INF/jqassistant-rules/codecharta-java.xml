<jqassistant-rules xmlns="http://schema.jqassistant.org/rule/v2.2"
                   xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
                   xsi:schemaLocation="http://schema.jqassistant.org/rule/v2.2 https://jqassistant.github.io/jqassistant/current/schema/jqassistant-rule-v2.2.xsd">

    <concept id="codecharta-java:TypeAggregationLevel">
        <providesConcept refId="codecharta:AggregationLevel"/>
        <requiresConcept refId="java:GeneratedType"/>
        <requiresConcept refId="java:InnerType"/>
        <description>Provides Java types as aggregation level.</description>
        <cypher><![CDATA[
            MERGE
              (aggregationLevel:CodeCharta:AggregationLevel{name:":Java:Type"})
            WITH
              aggregationLevel
            MATCH
              (:Artifact)-[:CONTAINS]->(metricsType:Java:Type)
            WHERE NOT
              metricsType:Inner
            WITH
              aggregationLevel, metricsType
            MATCH
              (metricsType)-[:DECLARES*0..]->(aggregatedType:Java:Type)
            WHERE NOT
              aggregatedType:Generated
            MERGE
              (metricsType)-[:HAS_METRICS]->(nodeMetrics:CodeCharta:Node:Metrics)
            MERGE
              (aggregationLevel)-[:CONTAINS]->(nodeMetrics)
            MERGE
              (nodeMetrics)-[:AGGREGATES]->(aggregatedType)
            RETURN
              aggregationLevel as AggregationLevel, count(nodeMetrics) AS NodeMetrics
            ]]></cypher>
    </concept>

    <concept id="codecharta-java:TypeReport">
        <requiresConcept refId="codecharta:Metrics"/>
        <description>Creates a CodeCharta report for Java types.</description>
        <cypher><![CDATA[
            MATCH
              (type:Java:Type)-[:HAS_METRICS]->(:CodeCharta:Metrics),
              tree = shortestPath((root:Java:Package)-[:CONTAINS*]->(type))
            WHERE NOT (
              type:Generated
              or (:Package)-[:CONTAINS]->(root) // identify the root package
            )
            WITH
              nodes(tree) as nodes
            UNWIND
              nodes as node // filter out duplicate packages
            WITH
              distinct node
            OPTIONAL MATCH
              (parentNode:Package)-[:CONTAINS]->(node)
            WITH
              node, parentNode
            OPTIONAL MATCH
              (node)-[:HAS_METRICS]->(nodeMetrics:CodeCharta:Node:Metrics)
            WITH
              node, parentNode, nodeMetrics
            OPTIONAL MATCH
              (nodeMetrics)-[edgeMetrics:HAS_EDGE_METRICS]->(toNodeMetrics:CodeCharta:Node:Metrics)
            RETURN
              node as Node, node.name as NodeLabel, parentNode as ParentNode, nodeMetrics as NodeMetrics, collect(edgeMetrics) as EdgeMetrics
        ]]></cypher>
        <report type="codecharta"/>
    </concept>

    <concept id="codecharta-java:MavenProjectAggregationLevel">
        <providesConcept refId="codecharta:AggregationLevel"/>
        <description>Provides empty node and edge metrics for Maven modules.</description>
        <cypher><![CDATA[
            MERGE
              (aggregationLevel:CodeCharta:AggregationLevel{name:":Maven:Project"})
            WITH
              aggregationLevel
            MATCH
              (project:Maven:Project:Directory)
            MERGE
              (project)-[:HAS_METRICS]->(nodeMetrics:CodeCharta:Node:Metrics)
            MERGE
              (aggregationLevel)-[:CONTAINS]->(nodeMetrics)
            WITH
              project, aggregationLevel, nodeMetrics
            MATCH
              (project)-[:CREATES]->(:Artifact)-[:CONTAINS]->(type:Java:Type)
            WHERE NOT
              type:Generated
            MERGE
              (nodeMetrics)-[:AGGREGATES]->(type)
            RETURN
              aggregationLevel as AggregationLevel, count(distinct nodeMetrics) AS NodeMetrics
            ]]></cypher>
    </concept>

    <concept id="codecharta-java:MavenReport">
        <requiresConcept refId="codecharta:Metrics"/>
        <description>Creates a CodeCharta report for Maven modules.</description>
        <cypher><![CDATA[
            MATCH
              (node:Maven:Project:Directory)-[:HAS_METRICS]->(nodeMetrics:CodeCharta:Node:Metrics)
            OPTIONAL MATCH
              (parentNode:Maven:Project:Directory)-[:HAS_MODULE]->(node)
            OPTIONAL MATCH
              (nodeMetrics)-[edgeMetrics:HAS_EDGE_METRICS]->(toNodeMetrics:CodeCharta:Node:Metrics)
            RETURN
              node AS Node, node.fqn AS NodeLabel, parentNode AS ParentNode, nodeMetrics as NodeMetrics, collect(edgeMetrics) as EdgeMetrics
            ]]></cypher>
        <report type="codecharta"/>
    </concept>

    <concept id="codecharta-java:NumberOfTypesMetrics">
        <providesConcept refId="codecharta:Metrics"/>
        <requiresConcept refId="codecharta:AggregationLevel"/>
        <description>Provides numberOfTypes metrics per aggregation level.</description>
        <cypher><![CDATA[
            MATCH
              (nodeMetrics:CodeCharta:Node:Metrics)-[:AGGREGATES]->(type:Java:Type)
            WITH
              nodeMetrics, count(type) as numberOfTypes
            SET
              nodeMetrics.numberOfTypes = numberOfTypes
            RETURN
              count(nodeMetrics) as NodeMetrics
            ]]></cypher>
    </concept>

    <concept id="codecharta-java:SizeAndComplexityMetrics">
        <providesConcept refId="codecharta:Metrics"/>
        <requiresConcept refId="codecharta:AggregationLevel"/>
        <description>Provides rloc and complexity metrics per aggregation level.</description>
        <cypher><![CDATA[
            MATCH
              (nodeMetrics:CodeCharta:Node:Metrics)-[:AGGREGATES]->(type:Java:Type)-[:DECLARES]->(method:Java:Method)
            WITH
              nodeMetrics, sum(method.effectiveLineCount) AS rloc, sum(method.cyclomaticComplexity) AS complexity
            SET
              nodeMetrics.rloc = rloc
            SET
              nodeMetrics.complexity = complexity
            RETURN
              count(nodeMetrics) AS NodeMetrics
            ]]></cypher>
        <verify>
            <aggregation/>
        </verify>
    </concept>

    <concept id="codecharta-java:FanInMetrics">
        <providesConcept refId="codecharta:Metrics"/>
        <requiresConcept refId="codecharta:AggregationLevel"/>
        <description>Provides fanIn metrics per aggregation level.</description>
        <cypher><![CDATA[
            MATCH
              (aggregationLevel:CodeCharta:AggregationLevel),
              (aggregationLevel)-[:CONTAINS]->(nodeMetrics:CodeCharta:Node:Metrics)-[:AGGREGATES]->(type:Type),
              (aggregationLevel)-[:CONTAINS]->(dependentNodeMetrics:CodeCharta:Node:Metrics)-[:AGGREGATES]->(dependentType:Type),
              (dependentType:Type)-[dependsOn:DEPENDS_ON]->(type)
            WHERE
              nodeMetrics <> dependentNodeMetrics
            WITH
              nodeMetrics, count(dependsOn) AS fanIn, sum(dependsOn.weight) as fanInTotal
            SET
              nodeMetrics.fanIn = fanIn
            SET
              nodeMetrics.fanInTotal = fanInTotal
            RETURN
              count(nodeMetrics) AS NodeMetrics
            ]]></cypher>
        <verify>
            <aggregation/>
        </verify>
    </concept>

    <concept id="codecharta-java:FanOutMetrics">
        <providesConcept refId="codecharta:Metrics"/>
        <requiresConcept refId="codecharta:AggregationLevel"/>
        <description>Provides fanOut node metrics per aggregation level.</description>
        <cypher><![CDATA[
            MATCH
              (aggregationLevel:CodeCharta:AggregationLevel),
              (aggregationLevel)-[:CONTAINS]->(nodeMetrics:CodeCharta:Node:Metrics)-[:AGGREGATES]->(type:Type),
              (aggregationLevel)-[:CONTAINS]->(dependencyNodeMetrics:CodeCharta:Node:Metrics)-[:AGGREGATES]->(dependencyType:Type),
              (type:Type)-[dependsOn:DEPENDS_ON]->(dependencyType)
            WHERE
              nodeMetrics <> dependencyNodeMetrics
            WITH
              nodeMetrics, count(dependsOn) AS fanOut, sum(dependsOn.weight) as fanOutTotal
            SET
              nodeMetrics.fanOut = fanOut
            SET
              nodeMetrics.fanOutTotal = fanOutTotal
            RETURN
              count(nodeMetrics) AS NodeMetrics
            ]]></cypher>
        <verify>
            <aggregation/>
        </verify>
    </concept>

    <concept id="codecharta-java:CouplingMetrics">
        <providesConcept refId="codecharta:Metrics"/>
        <requiresConcept refId="codecharta:AggregationLevel"/>
        <description>Provides FanOut node and edge metrics for Java types.</description>
        <cypher><![CDATA[
            MATCH
              (aggregationLevel:CodeCharta:AggregationLevel),
              (aggregationLevel)-[:CONTAINS]->(fromNodeMetrics:CodeCharta:Node:Metrics)-[:AGGREGATES]->(type:Type),
              (aggregationLevel)-[:CONTAINS]->(toNodeMetrics:CodeCharta:Node:Metrics)-[:AGGREGATES]->(dependencyType:Type),
              (type)-[dependsOn:DEPENDS_ON]->(dependencyType)
            WHERE
              fromNodeMetrics <> toNodeMetrics
            WITH
              fromNodeMetrics, count(dependsOn) as coupling, sum(dependsOn.weight) as totalCoupling, toNodeMetrics
            CALL {
              WITH
                fromNodeMetrics, coupling, totalCoupling, toNodeMetrics
              MERGE
                (fromNodeMetrics)-[edgeMetrics:HAS_EDGE_METRICS]->(toNodeMetrics)
              SET
                edgeMetrics.coupling = coupling
              SET
                edgeMetrics.totalCoupling = totalCoupling
              RETURN
                edgeMetrics
            } IN TRANSACTIONS
            RETURN
              count(edgeMetrics) AS EdgeMetrics
            ]]></cypher>
        <verify>
            <aggregation/>
        </verify>
    </concept>

    <concept id="codecharta-java:TypeHasGitSourceFile">
        <description>Creates a relation HAS_SOURCE_FILE between a Java type and the file from the Git history.</description>
        <cypher><![CDATA[
            MATCH
              (artifact:Java:Artifact)-[:CONTAINS]->(type:Type)
            // determine source file name from class name
            WITH
              type, split(type.fileName,"/") as segments
            WITH
              type, segments, range(0, size(segments) -2) AS indexes
            WITH
              type, reduce(path = "", index in indexes | path + segments[index] + "/") + type.sourceFileName as sourceFileName
            // find file in Git history that ends with the source file name
            MATCH
              (file:Git:File)
            WHERE
              file.relativePath ends with sourceFileName
            CALL {
              WITH
                type, file
              MERGE
                (type)-[:HAS_SOURCE_FILE]->(file)
            } IN TRANSACTIONS
            RETURN
              count(type) as TypesWithSourceFile
        ]]></cypher>
        <verify>
            <aggregation/>
        </verify>
    </concept>

    <concept id="codecharta-java:NumberOfCommitsMetrics">
        <providesConcept refId="codecharta:Metrics"/>
        <requiresConcept refId="codecharta:AggregationLevel"/>
        <requiresConcept refId="codecharta-java:TypeHasGitSourceFile"/>
        <description>Provides NumberOfCommits node metrics for Java types.</description>
        <cypher><![CDATA[
            MATCH
              (nodeMetrics:CodeCharta:Node:Metrics)-[:AGGREGATES]->(type:Java:Type),
              (type)-[:HAS_SOURCE_FILE]->(gitFile:Git:File),
              (commit:Git:Commit)-[:CONTAINS_CHANGE]->(:Git:Change)-[:MODIFIES]->(gitFile)
            WITH
              nodeMetrics, count(distinct commit) as numberOfCommits
            SET
              nodeMetrics.numberOfCommits = numberOfCommits
            RETURN
              count(nodeMetrics) AS NodeMetrics
            ]]></cypher>
        <verify>
            <aggregation/>
        </verify>
    </concept>

    <concept id="codecharta-java:NumberOfAuthorsMetrics">
        <providesConcept refId="codecharta:Metrics"/>
        <requiresConcept refId="codecharta:AggregationLevel"/>
        <requiresConcept refId="codecharta-java:TypeHasGitSourceFile"/>
        <description>Provides NumberOfAuthors node metrics for Java types.</description>
        <cypher><![CDATA[
            MATCH
              (nodeMetrics:CodeCharta:Node:Metrics)-[:AGGREGATES]->(type:Java:Type),
              (type)-[:HAS_SOURCE_FILE]->(gitFile:Git:File),
              (author:Git:Author)-[:COMMITTED]->(:Git:Commit)-[:CONTAINS_CHANGE]->(:Git:Change)-[:MODIFIES]->(gitFile)
            WITH
              nodeMetrics, count(distinct author) as numberOfAuthors
            SET
              nodeMetrics.numberOfAuthors = numberOfAuthors
            RETURN
              count(nodeMetrics) AS NodeMetrics
        ]]></cypher>
        <verify>
            <aggregation/>
        </verify>
    </concept>

</jqassistant-rules>
